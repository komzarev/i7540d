VERSION_BUILD = $$(BUILD_NUMBER)
isEmpty(VERSION_BUILD) {
    VERSION_BUILD = 1
}

VERSION_TAG = $$(GIT_RELEASE_TAG)
isEmpty(VERSION_TAG) {
    VERSION_TAG = v0.0.0
}

equals(QT_MAJOR_VERSION, 5):!lessThan(QT_MINOR_VERSION, 8) {
    VERSION_WITHOUT_V = $$str_member($${VERSION_TAG}, 1, -1)
} else {
    VERSION_WITHOUT_V = $$replace(VERSION_TAG, "v", "")
}

VERSIONS = $$split(VERSION_WITHOUT_V, ".")
VERSION_MAJOR = $$member(VERSIONS, 0)
VERSION_MINOR = $$member(VERSIONS, 1)
VERSION_PATCH = $$member(VERSIONS, 2)

win32 {
    VERSION_FULL = $${VERSION_MAJOR}.$${VERSION_MINOR}.$${VERSION_PATCH}.$${VERSION_BUILD}
}
else: VERSION_FULL = $${VERSION_MAJOR}.$${VERSION_MINOR}.$${VERSION_PATCH}
if(equals(QT_MAJOR_VERSION, 5):!lessThan(QT_MINOR_VERSION, 12)) {
    VERSION = $${VERSION_FULL}
}
DEFINES += VERSION_STRING=\\\"$${VERSION_FULL}\\\"
CONFIG += skip_target_version_ext

